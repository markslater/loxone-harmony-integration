/*
 * Copyright 2017 Mark Slater
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of the License at
 *
 * 	http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'pmd'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'com.github.spotbugs' version '4.2.1'
    id 'nebula.ospackage' version '8.4.1'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'net.sourceforge.argo', name: 'argo', version: '5.13'
    implementation group: 'net.sourceforge.urin', name: 'urin', version: '3.11'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    implementation group: 'net.sourceforge.sorb', name: 'sorb', version: '0.8'
    implementation group: 'net.sourceforge.spinnerule', name: 'spinnerule', version: '0.31'

    implementation group: 'org.igniterealtime.smack', name: 'smack-core', version: '4.2.0'
    implementation group: 'org.igniterealtime.smack', name: 'smack-java7', version: '4.2.0'
    implementation group: 'org.igniterealtime.smack', name: 'smack-tcp', version: '4.2.0'
    implementation group: 'org.igniterealtime.smack', name: 'smack-debug', version: '4.2.0'
    runtimeOnly group: 'org.igniterealtime.smack', name: 'smack-sasl-javax', version: '4.2.0'

    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version :'5.6.2'
    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.0'

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
}

pmd {
    ruleSetFiles = files('tools/pmd-ruleset.xml')
    ruleSets = []
}

tasks.withType(Test) {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'harmonyintegration.LoxoneHarmonyIntegration'
    }
}

task makeDeb(type: Deb) {
    packageName = 'loxone-harmony-integration'
    version = '1.2.3'
    release = 1

    requires('openjdk-8-jre')

    postInstall file('deploy/lifecycle/postInstall.sh')
    preUninstall file('deploy/lifecycle/preUninstall.sh')

    into 'opt/loxone-harmony-integration'

    from(shadowJar.outputs.files)

    from('deploy/distributed') {
        into('/etc/systemd/system')
    }
}